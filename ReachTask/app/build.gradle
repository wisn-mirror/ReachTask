apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.wisn.reachtask"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode getGitVersion()
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            Properties properties=new Properties()
            InputStream inputStream= rootProject.file('local.properties').newDataInputStream()
            properties.load(inputStream)

            storeFile file(properties.getProperty('RELEASE_STOREFILE'))
            keyAlias properties.getProperty('RELEASE_KEY_ALIAS')
            keyPassword properties.getProperty('RELEASE_KEY_PASSWORD')
            storePassword properties.getProperty('RELEASE_STOREPASSWORD')
        }
    }
    buildTypes {
        release {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
        }
    }

    flavorDimensions 'product', 'channel'
    productFlavors{

        ///////////////////////////////product/////////////////////////////////////////
        build_debug {
            dimension 'product'
        }

        // 产生测试环境的产品
        build_test {
            dimension 'product'
        }

        // 产生生产环境环境的产品
        build_release {
            dimension 'product'
        }

        ///////////////////////////////version/////////////////////////////////////////
        channel_1{
            dimension 'channel'
//            manifestPlaceholders= [ UMENG_CHANNEL:"flavor_a",CName_value:properties.getProperty("CNameAAA"), WisnTheme:"@style/AppTheme1","icon":"@mipmap/ic_launcher" ]
        }

        channel_2{
            dimension 'channel'
//            manifestPlaceholders= [  UMENG_CHANNEL:"flavor_b",CName_value:properties.getProperty("CNameBBB"),WisnTheme:"@style/AppTheme2","icon":"@mipmap/want" ]
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def buildType = variant.buildType.name
            def outputFile = output.outputFile
            def date = releaseTime()
            def applicationId = defaultConfig.applicationId
            def versionName = defaultConfig.versionName
            def versionCode = defaultConfig.versionCode
            if (outputFile != null && buildType.contains("release") && outputFile.name.endsWith('.apk')) {
                output.outputFileName= "${applicationId}_${variant.flavorName}_${versionCode}_${versionName}_${date}.apk"
//                output.outputFile = new File(outputFile.parent, fileName)
//                output.outputFile = new File("$rootDir/apkFile", fileName)  //3.0之前的
            }
        }
    }
}

def releaseTime() {
    return new Date().format("yyyy-MM-dd_hh-mm-ss", TimeZone.getTimeZone("Asia/Shanghai"))
}

def getGitVersion() {
    return 'git rev-list HEAD --count'.execute().text.trim().toInteger()
}
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':mainmodule')
}
